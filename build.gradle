subprojects {
    plugins.apply("java-library")
    plugins.apply("maven-publish")

    group = 'org.popcraft'

    project.ext.majorVersion = '1'
    project.ext.minorVersion = '2'
    project.ext.patchVersion = commitsSinceLastTag()
    project.ext.fullVersion = project.ext.majorVersion + '.' + project.ext.minorVersion + '.' + project.ext.patchVersion
    version = project.ext.fullVersion

    [tasks.withType(JavaCompile), tasks.withType(Javadoc)].forEach { type ->
        type.configureEach {
            it.options.encoding = "UTF-8"
        }
    }

    java {
        withSourcesJar()
        def targetJava = 8
        targetCompatibility = JavaVersion.toVersion(targetJava)
        sourceCompatibility = JavaVersion.toVersion(targetJava)
    }

    repositories {
        mavenCentral()
        maven {
            url 'https://oss.sonatype.org/content/repositories/snapshots'
        }
    }
}

String commitsSinceLastTag() {
    def tagDescription = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = tagDescription
    }
    tagDescription = tagDescription.toString()
    if (tagDescription.indexOf('-') < 0) {
        return 0;
    }
    return tagDescription.split('-')[1]
}
