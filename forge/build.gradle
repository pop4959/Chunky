buildscript {
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
    }
}

plugins {
    id 'com.github.johnrengelman.shadow'
}

plugins.apply("net.minecraftforge.gradle")

group = 'org.popcraft'

minecraft {
    mappings channel: 'snapshot', version: '20210309-1.16.5'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    compile.extendsFrom shade
}

dependencies {
    shade(project(":common"))
    minecraft 'net.minecraftforge:forge:1.16.5-36.1.13'
}

tasks {
    shadowJar {
        archiveFileName.set("Chunky-Forge-${project.version}.jar")
        configurations = [project.configurations.shade]
    }
    build {
        dependsOn(shadowJar)
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chunky",
                "Specification-Vendor"    : "pop4959",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.version,
                "Implementation-Vendor"   : "pop4959",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

afterEvaluate {
    reobf {
        shadowJar {
            mappings = createMcpToSrg.output
        }
    }
}
