// This block is required by Fabric
pluginManagement {
    repositories {
        gradlePluginPortal()
        maven { url "https://maven.fabricmc.net" }
        maven { url "https://files.minecraftforge.net/maven" }
    }

    // default plugin versions
    plugins {
        // todo: Bump to 7.0.0 (or latest) when updating to Gradle 7
        id("com.github.johnrengelman.shadow") version "6.1.0"
    }
}

rootProject.name = 'chunky'

include('common')

[
        'bukkit',
        'sponge',
        'forge',
        'fabric'
].forEach {
    platform(it)
}

void platform(String name) {
    include("chunky-$name")
    project(":chunky-$name").projectDir = file(name)
}
