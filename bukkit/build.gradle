plugins {
    id 'com.github.johnrengelman.shadow'
}

repositories {
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://papermc.io/repo/repository/maven-public'
    }
}

dependencies {
    implementation project(':common')

    compileOnly 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'
    implementation 'io.papermc:paperlib:1.0.6'
}

processResources {
    filesMatching("plugin.yml") {
        expand 'projectVersion': project.ext.fullVersion
    }
}

jar {
    archiveClassifier.set("unshaded")
}

shadowJar {
    archiveClassifier.set(null)
    archiveFileName.set("Chunky-${project.ext.fullVersion}.jar")

    relocate 'io.papermc.lib', 'org.popcraft.chunky.lib.paperlib'
}

artifacts {
    archives shadowJar
}

publishing {
    repositories {
        if (project.hasProperty('mavenUsername') && project.hasProperty('mavenPassword'))
            maven {
                credentials {
                    username = project.mavenUsername
                    password = project.mavenPassword
                }
                def releasesRepoUrl = 'https://repo.codemc.io/repository/maven-releases/'
                def snapshotsRepoUrl = 'https://repo.codemc.io/repository/maven-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'org.popcraft'
            artifactId = 'chunky-bukkit'
            version = project.ext.fullVersion
            from components.java
        }
    }
}
