plugins {
    id 'com.github.johnrengelman.shadow'
    id 'fabric-loom' version '0.7-SNAPSHOT'
    id 'maven-publish'
}

configurations {
    implementation.extendsFrom shade
}

repositories {
    mavenLocal()
}

dependencies {
    shade(project(':common'))

    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    def targetJava = 16
    //targetCompatibility = JavaVersion.toVersion(targetJava)
    //sourceCompatibility = JavaVersion.toVersion(targetJava)

    // todo: When updating to Gradle 7 and Loom 0.8+, remove and replace toolchains usage
    // here and in the root project build.gradle with the above commented out
    // source and target compatibility configuration.
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJava)
    }
}

tasks {
    jar {
        archiveClassifier.set("dev")
    }
    shadowJar {
        archiveClassifier.set("shadow")
        configurations = [project.configurations.shade]
    }
    remapJar {
        archiveClassifier.set(null)
        input.set(shadowJar.archiveFile)
        archiveFileName.set("Chunky-${project.version}.jar")
    }
    build {
        dependsOn(remapJar)
    }
}
